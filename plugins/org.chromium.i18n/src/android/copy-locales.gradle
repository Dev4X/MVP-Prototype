// Copyright (c) 2015 The Chromium Authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

// This custom copy step is necessary because _locales begins with an underscore :(
// https://code.google.com/p/android/issues/detail?id=161356
cdvPluginPostBuildExtras.add({
    if (!file('assets/www/_locales').exists()) {
        return
    }
    android.applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def mergeTask = tasks["merge${variantName}Assets"]
        def processTask = tasks["process${variantName}Resources"]
        def newTask = project.task("copyLocaleAssets${variantName}", type: Copy) {
            from "assets/www/_locales"
            into "${mergeTask.outputDir}/www/_locales"
        }
        newTask.dependsOn(mergeTask)
        processTask.dependsOn(newTask)
    }
    android.aaptOptions.ignoreAssetsPattern = "!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~"
})
